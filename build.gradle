plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def lwjgl_version="3.2.2"
def breakoutapi_version="0a8061b"

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven{ url="https://jitpack.io" }
    maven{ url="http://server.bbkr.space/artifactory/libs-release/" }
    maven { url='https://repo.repsy.io/mvn/xxx_inaksis_xxx/inaksis' }
}

minecraft {
    accessWidener = file("src/main/resources/obsidian.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation ("com.lettuce.fudge:artifice:0.15.2+21w07a") { exclude group: "net.fabricmc.fabric-api" }
    include ("com.lettuce.fudge:artifice:0.15.2+21w07a") { exclude group: "net.fabricmc.fabric-api" }

    modImplementation ("io.github.vampirestudios:VampireLib-Fabric:2.4.0+build.1-21w11a") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "io.github.cottonmc"
    }
    include ("io.github.vampirestudios:VampireLib-Fabric:2.4.0+build.1-21w11a") {
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "io.github.cottonmc"
    }

    // INclude GeckoLib for each module, uses experimental 1.17 snapshot fork.
    modImplementation 'software.bernie.geckolib:geckolib-fabric-1.17:3.0.20:dev'

    modCompile "com.github.raphydaphy:breakoutapi:$breakoutapi_version"
    include "com.github.raphydaphy:breakoutapi:$breakoutapi_version"

    implementation "org.lwjgl:lwjgl-nanovg:$lwjgl_version"
    runtimeOnly "org.lwjgl:lwjgl-nanovg:$lwjgl_version:natives-windows"
    runtimeOnly "org.lwjgl:lwjgl-nanovg:$lwjgl_version:natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-nanovg:$lwjgl_version:natives-macos"

    implementation "org.lwjgl:lwjgl-yoga:$lwjgl_version"
    runtimeOnly "org.lwjgl:lwjgl-yoga:$lwjgl_version:natives-windows"
    runtimeOnly "org.lwjgl:lwjgl-yoga:$lwjgl_version:natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-yoga:$lwjgl_version:natives-macos"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {}
}
